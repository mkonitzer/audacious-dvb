dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.65)
AC_INIT([audacious-dvb], [0.8.0])
AM_INIT_AUTOMAKE
LT_INIT

AC_CONFIG_HEADERS([src/config.h])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_FILES([Makefile src/Makefile examples/Makefile])

AC_PROG_CC
AC_HEADER_STDC

# GLIB
AM_PATH_GLIB_2_0(2.6.0, , AC_MSG_ERROR([*** GLIB >= 2.6.0 not installed - please install first ***]))

# GTK+
AM_PATH_GTK_2_0(2.6.0, , AC_MSG_ERROR([*** GTK+ >= 2.6.0 not installed - please install first ***]), gthread)

# Audacious
PKG_CHECK_MODULES(AUDACIOUS, [audacious >= 1.5.0], ,
	AC_MSG_ERROR([*** Audacious >= 1.5.0 is not installed - please install first ***]))
SAVED_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS $AUDACIOUS_CFLAGS"
SAVED_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $AUDACIOUS_CFLAGS"
AC_CHECK_HEADERS([audacious/misc.h])
AC_CHECK_TYPE([mcs_handle_t],
    AC_DEFINE([HAVE_MCS_HANDLE_T], 1,
	[Define to 1 if your Audacious has the type `mcs_handle_t'.]),
    [],	[[#include <audacious/configdb.h>]])
AC_MSG_CHECKING([for Audacious input plugin directory])
AC_ARG_WITH(plugindir,
	AS_HELP_STRING([--with-plugindir=DIR],[set the directory where Audacious-DVB will be installed @<:@default=autodetect@:>@]),
	[ case "$withval" in
	    [[\\/$]]*)
		plugindir="$withval"
		;;
	    *)
		AC_MSG_RESULT([failed])
		AC_MSG_ERROR([expected an absolute directory name for --with-plugindir: $withval])
		;;
	esac ],
	[plugindir="`$PKG_CONFIG --variable=input_plugin_dir audacious`"])
AC_MSG_RESULT([$plugindir])
AC_SUBST(PLUGIN_DIR, "$plugindir")
CPPFLAGS="$SAVED_CPPFLAGS"
CFLAGS="$SAVED_CFLAGS"

# libglade
PKG_CHECK_MODULES(LIBGLADE, [libglade-2.0 >= 2.3.1], ,
	AC_MSG_ERROR([*** libglade >= 2.3.1 is not installed - please install first ***]))

# libmad
PKG_CHECK_MODULES(LIBMAD, [mad >= 0.15.0], , AC_MSG_ERROR([*** libmad >= 0.15.0 is not installed - please install first ***]))

AC_OUTPUT
